#
# $Id: SConscript,v 1.3 2010/05/18 21:53:18 kutschke Exp $
# $Author: kutschke $
# $Date: 2010/05/18 21:53:18 $
#
# Original author Rob Kutschke.
#

import os.path, re
Import('env')

plugin_cc = Glob('*_plugin.cc', strings=True)
cxx_files = Glob('*.cxx', strings=True)

if cxx_files:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name=os.path.dirname(dd)

    env.SharedLibrary('#/lib/'+local_lib_name, cxx_files,
                       LIBS=[ 'FWCoreFramework',
                              'CLHEP',
                              'Reflex',
                              'Core',
                              'EG',
                              'Graf3d',
                              'Graf',
                              'Gpad',
                              'Hist',
                              'Matrix',
                              'FFTW',
                              'RecoBase',
                              'RawData',
                              'Geometry',
                              ],
                        )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc, LIBS=['FWCoreFramework',
                                          libs,
                                          'CLHEP',
                                          'Reflex',
                                          'Core',
                                          'EG',
                                          'Graf3d',
                                          'Graf',
                                          'Gpad',
                                          'Hist',
                                          'Matrix',
                                          'FFTW',
                                          'RecoBase',
                                          'RawData',
                                          'Geometry',
                                          ] )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
