#
# $Id: SConscript,v 1.3 2010/05/18 21:53:18 kutschke Exp $
# $Author: kutschke $
# $Date: 2010/05/18 21:53:18 $
#
# Original author Rob Kutschke.
#

import os.path, re
Import('env')

genie   = os.environ['GENIE_DIR'  ]
xml2    = os.environ['LIBXML2_DIR']
log4cpp = os.environ['LOG4CPP_DIR']
lhapdf  = os.environ['LHAPDF_DIR' ]
pythia  = os.environ['PYTHIA_DIR' ]
cry     = os.environ['CRY_DIR'    ]

GENIELIBS = [
	'xml2', 		'log4cpp',		'LHAPDF',
	'GMessenger',		'GRegistry',		'GAlgorithm',
	'GNumerical',		'GInteraction',		'GUtils',
	'GBase',		'GPDG',			'GPDF',
	'GDecay',		'GFragmentation',	'GBaryonResonance',
	'GNuclear',		'GElFF',		'GLlewellynSmith',
	'GCrossSections',	'GReinSeghal',		'GPaschos',		
	'GHEP',			'GEVGCore',		'GEVGModules',		
	'GEVGDrivers',		'GVHE',			'GQPM',
	'GBodekYang',		'GHadronTransp',	'GCharm',
	'GElas',		'GQEL',			'GRES',
	'GDIS',			'GMEC',			'GMuELoss',
	'GCoh',			'GDecay',		'GDfrc',
	'GNuE',			'GNuGamma',		'GReWeight',
	'GNtuple',		'GGeo',			'GFluxDrivers',
]


# last two components of the path. Ex: /not/this/but/THIS/AND_THIS
#                                      "AND_THIS" is usually "src"
dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
ff=os.path.dirname(dd)   # THIS

my_lib=[]  # default if no cxx_files
cxx_files = Glob('*.cxx',strings=True)
if cxx_files:
    env.SharedLibrary( '#/lib/%s'%(ff,), cxx_files,
                       # common libs
                       LIBS=[ 'FWCoreFramework',
                              'FWCoreUtilities',
                              'FWCoreServiceRegistry',
                              'boost_thread-gcc%s-mt'%(env.gcc_ver,),
                              'Reflex',
                              'Core',
			      'Geom',
	                      'Hist',
                              'Physics',
			      'Pythia6',	
			      'EG',
			      'EGPythia6',
			      'Tree',
                              'xml2',
                              'log4cpp',
                              'LHAPDF',
	                      'CRY',
			      GENIELIBS,
                              'SimulationBase',
                            ],
			parse_flags=[ '-L'+log4cpp+'/lib', '-I'+log4cpp+'/include',
				      '-L'+xml2+'/lib',    '-I'+xml2+'/include',
				      '-L'+lhapdf+'/lib',  '-I'+lhapdf+'/include',
				      '-L'+pythia+'/lib',  '-I'+pythia+'/include',
				      '-L'+genie+'/lib',   '-I'+genie+'/include/GENIE/',
				      '-L'+cry+'/lib',     '-I'+cry+'/src',
	                            ]					
        )
    my_lib=['%s'%(ff,)]


# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
